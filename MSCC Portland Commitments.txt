SELECT DISTINCT
    b.BibliographicRecordID,
    b.BrowseTitle,
    b.BrowseAuthor,
    pub.Publisher,
    oclc.[035Field],
    cir.ItemRecordID,
    ird.CallNumber,
    cir.LifetimeCircCount,
    cir.LifetimeRenewalsCount,

    CASE cir.AssignedBranchID
        WHEN 5  THEN 'PDTN'
        WHEN 6  THEN 'PBUR'
        WHEN 7  THEN 'PPKS'
        WHEN 8  THEN 'PRIV'
        WHEN 9  THEN 'PBKM'
        WHEN 10 THEN 'PWHS'
        WHEN 17 THEN 'POUT'
        WHEN 18 THEN 'PILL'
        ELSE 'Error'
    END AS PPLBranch,

    CONCAT(cir.FreeTextBlock, '|', ird.NonPublicNote) AS Notes,

    other.OtherBranchName,
    other.OtherBranchNotes

FROM CircItemRecords cir WITH (NOLOCK)

JOIN ItemRecordDetails ird WITH (NOLOCK)
    ON cir.ItemRecordID = ird.ItemRecordID

JOIN BibliographicRecords b WITH (NOLOCK)
    ON cir.AssociatedBibRecordID = b.BibliographicRecordID

OUTER APPLY (
    SELECT TOP (1)
        bs.Data AS Publisher
    FROM BibliographicTags bt WITH (NOLOCK)
    JOIN BibliographicSubfields bs WITH (NOLOCK)
        ON bt.BibliographicTagID = bs.BibliographicTagID
    WHERE bt.BibliographicRecordID = b.BibliographicRecordID
      AND bt.TagNumber IN (264, 260)
      AND bs.Subfield = 'b'
    ORDER BY
        CASE
            WHEN bt.TagNumber = 264 THEN 1
            WHEN bt.TagNumber = 260 THEN 2
        END
) pub

OUTER APPLY (
    SELECT TOP (1)
        bs.Data AS [035Field]
    FROM BibliographicTags bt WITH (NOLOCK)
    JOIN BibliographicSubfields bs WITH (NOLOCK)
        ON bt.BibliographicTagID = bs.BibliographicTagID
    WHERE bt.BibliographicRecordID = b.BibliographicRecordID
      AND bt.TagNumber = 035
      AND bs.Subfield = 'a'
      AND (
            bs.Data LIKE '%OCoLC%'
         OR bs.Data LIKE '%ocn%'
         OR bs.Data LIKE '%ocm%'
      )
) oclc

LEFT JOIN (
    SELECT
        cir2.AssociatedBibRecordID,

        STUFF((
            SELECT DISTINCT
                ', ' +
                CASE cir3.AssignedBranchID
                    WHEN 14 THEN 'MSL'
                    WHEN 3  THEN 'BPL'
                END
            FROM CircItemRecords cir3
            WHERE cir3.AssociatedBibRecordID = cir2.AssociatedBibRecordID
              AND cir3.AssignedBranchID IN (14, 3)
            FOR XML PATH(''), TYPE
        ).value('.', 'NVARCHAR(MAX)'), 1, 2, '') AS OtherBranchName,

        STUFF((
            SELECT DISTINCT
                ' || ' + CONCAT(cir4.FreeTextBlock, '|', ird4.NonPublicNote)
            FROM CircItemRecords cir4
            JOIN ItemRecordDetails ird4
                ON cir4.ItemRecordID = ird4.ItemRecordID
            WHERE cir4.AssociatedBibRecordID = cir2.AssociatedBibRecordID
              AND cir4.AssignedBranchID IN (14, 3)
              AND (
                    cir4.FreeTextBlock LIKE '%MSCC%'
                 OR cir4.FreeTextBlock LIKE '%committed to retain%'
                 OR ird4.NonPublicNote LIKE '%MSCC%'
                 OR ird4.NonPublicNote LIKE '%committed to retain%'
              )
            FOR XML PATH(''), TYPE
        ).value('.', 'NVARCHAR(MAX)'), 1, 4, '') AS OtherBranchNotes

    FROM CircItemRecords cir2
    WHERE cir2.AssignedBranchID IN (14, 3)
    GROUP BY cir2.AssociatedBibRecordID

) other
    ON other.AssociatedBibRecordID = b.BibliographicRecordID

WHERE
      cir.AssignedBranchID IN (5, 6, 7, 8, 9, 10, 17, 18)
  AND (
        cir.FreeTextBlock LIKE '%MSCC%'
     OR cir.FreeTextBlock LIKE '%committed to retain%'
     OR ird.NonPublicNote LIKE '%MSCC%'
     OR ird.NonPublicNote LIKE '%committed to retain%'
  )

ORDER BY
    b.BibliographicRecordID,
    cir.ItemRecordID;